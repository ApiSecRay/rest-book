buildscript {
  repositories {
    maven { url 'http://repo.spring.io/plugins-release' }
  }
  
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion",
              'io.spring.gradle:docbook-reference-plugin:0.3.1'
  }
}

apply plugin: 'java'

sourceCompatibility = 1.8
group = 'com.github.remonsinnema'
version = '1.0.0'

task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
}

defaultTasks 'build'

repositories {
  maven { url 'https://repo.spring.io/release' }
  mavenCentral()
}

apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'

checkstyle {
  toolVersion = '7.3'
}

dependencies {
  compile 'org.springframework.boot:spring-boot-starter-web',
      'org.springframework.boot:spring-boot-starter-data-jpa',
      'org.springframework.hateoas:spring-hateoas:0.20.0.RELEASE',
      'com.jayway.jsonpath:json-path',
      'commons-io:commons-io:2.5',
      'de.escalon.hypermedia:hydra-spring:0.3.1',
      'org.json:json:20141113',
      'org.javamoney:moneta:1.1'
  testCompile 'junit:junit:4.12','org.jbehave:jbehave-core:4.1', 
      'org.springframework.boot:spring-boot-starter-test'
  runtime("com.h2database:h2")
}

jar {
  baseName = 'restbucks'
  version = '1.0.0'
}

test {
  outputs.upToDateWhen { false }
  doFirst {
    file('target').mkdirs() // Required by JBehave
  }
  doLast {
    // Prepare pretty HTML report
    def dir = file("$buildDir/reports/acceptance-tests")
    def resultDir = file('target/jbehave')
    ant.move(todir: dir.path) {
      fileset(dir: new File(resultDir, 'view').path, excludes: "**/*.xml.html")
    }
    def testResourcesDir = sourceSets.test.resources.srcDirs[0].path
    ant.copy(todir: dir.path) {
      fileset(dir: testResourcesDir, includes: "**/*.css")
      fileset(dir: testResourcesDir, includes: "**/*.png")
    }
    
    // Count results
    def numTested = 0
    def numFailed = 0
    def numPending = 0
    def failingScenarios = []
    resultDir.listFiles().findAll { it.name.endsWith('.xml') && !it.name.endsWith('Stories.xml') }.each {
      new XmlSlurper().parse(it).scenario.each {
        def failed = false
        def pending = false
        it.step.each {
          if (it.@outcome == 'failed') {
            failed = true
          } else if (it.@outcome == 'pending') {
            pending = true
          }
        }
        numTested++
        if (failed) {
          numFailed++
          failingScenarios.add it.@title
          it.failure.each { failingScenarios.add "- ${it.toString().split('\n')[0]}" }
        } else if (pending) {
          numPending++
        }
      }
    }
    def message = "Acceptance tests: $numTested scenarios, $numFailed failed, $numPending pending"
    ant.delete(dir: file('target').path, failonerror: false)
    if (numFailed > 0) {
      logger.quiet "$message\n${failingScenarios.join('\n')}"
    }
    println message
  }
}


apply plugin: 'eclipse'

eclipse {
  classpath {
    defaultOutputDir = file('classes')
  }
}


apply plugin: 'docbook-reference'

reference {
  sourceDir = file('src/main/docbook')
  pdfFilename = 'restbook.pdf'
  epubFilename = 'restbook.epub'
}

assemble.dependsOn reference
